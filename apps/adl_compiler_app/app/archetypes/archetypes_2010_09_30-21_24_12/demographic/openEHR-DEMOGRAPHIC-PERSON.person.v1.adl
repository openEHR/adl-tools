archetype (adl_version=1.4)
	openEHR-DEMOGRAPHIC-PERSON.person.v1

concept
	[at0000]	-- dados da pessoa
language
	original_language = <[ISO_639-1::pt-br]>
	translations = <
		["en"] = <
			language = <[ISO_639-1::en]>
			author = <
				["name"] = <"Sergio Miranda Freire">
				["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
				["email"] = <"sergio@lampada.uerj.br">
			>
		>
	>
description
	original_author = <
		["name"] = <"Sergio Miranda Freire & Rigoleta Dutra Mediano Dias">
		["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
		["email"] = <"sergio@lampada.uerj.br">
		["date"] = <"22/05/2009">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Representation of a person's demographic data.">
			use = <"Used in demographic service to collect a person's data.">
			keywords = <"demographic service", "person's data">
			misuse = <"">
			copyright = <"copyright (c) 2010 openEHR Foundation">
		>
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Representação dos dados demográficos de uma pessoa">
			use = <"Usado em serviço demográficos para coletar os dados de uma pessoa">
			keywords = <"serviço demográfico", "dados de uma pessoa">
			misuse = <"">
			copyright = <"copyright (c) 2010 openEHR Foundation">
		>
	>
	lifecycle_state = <"Authordraft">
	other_contributors = <>
	other_details = <
		["references"] = <"ISO/TS 22220:2008(E) - Identification of Subject of Care - Technical Specification - International Organization for Standardization.">
	>

definition
    PERSON[at0000] matches {  -- person demographic data
        details matches {
            ITEM_TREE[at0001] occurrences matches {0..1} matches {  -- Details
                items cardinality matches {0..*; ordered} matches {
                    allow_archetype CLUSTER[at0010] occurrences matches {0..*} matches {
                        include
                            archetype_id/value matches {/(person_additional_data)[a-zA-Z0-9_-]*\.v1/}
                            archetype_id/value matches {/(person_birth_data)[a-zA-Z0-9_-]*\.v1/}
                            archetype_id/value matches {/(person_death_data)[a-zA-Z0-9_-]*\.v1/}
                            archetype_id/value matches {/(person_identifier)[a-zA-Z0-9_-]*\.v1/}
                            archetype_id/value matches {/(biometric_identifier)[a-zA-Z0-9_-]*\.v1/}
                    }
                }
            }
        }
        identities cardinality matches {1..*; ordered} matches {
            allow_archetype PARTY_IDENTITY[at0002] occurrences matches {1..1} matches {
                include
                    archetype_id/value matches {/(person_name)[a-zA-Z0-9_-]*\.v1/}
            }
        }
        contacts cardinality matches {0..*; ordered} matches {
            CONTACT[at0003] occurrences matches {1..1} matches {  -- Contacts
                addresses cardinality matches {0..*; ordered} matches {
                    allow_archetype ADDRESS[at0030] occurrences matches {1..1} matches {
                        include
                            archetype_id/value matches {/(address)([a-zA-Z0-9_]+)*\.v1/}
                            archetype_id/value matches {/(electronic_communication)[a-zA-Z0-9_-]*\.v1/}
                    }
                }
            }
        }
        relationships cardinality matches {0..*; ordered} matches {
 	    PARTY_RELATIONSHIP[at0004] matches {	-- personal relationships
	 	details matches {
		    ITEM_SINGLE matches {	
			item matches {
		       	    ELEMENT[at0040] matches {	-- type of relationship
			      	value matches { 
				    DV_CODED_TEXT matches {
					defining_code matches {[ac0000]}
				    }		
				}
               		    }
			}
 		    }
		}
            }
        }
    }



ontology
	term_definitions = <
		["pt-br"] = <
			items = <
				["at0000"] = <
					text = <"dados da pessoa">
					description = <"dados da pessoa">
				>
				["at0001"] = <
					text = <"Detalhes">
					description = <"Detalhes demográficos da pessoa">
				>
				["at0002"] = <
					text = <"Nome">
					description = <"Conjunto de dados que especificam o nome da pessoa">
				>
				["at0003"] = <
					text = <"Contatos">
					description = <"Contatos da pessoa">
				>
				["at0004"] = <
					text = <"Relacionamentos">
					description = <"Relacionamentos de uma pessoa, especialmente laços familiares">
				>
				["at0010"] = <
					text = <"Nascimento, óbito, documentação e outros detalhes">
					description = <"Permite a coleta de dados sobre o nascimento, o óbito e/ou documentos de identificação da pessoa">
				>
				["at0030"] = <
					text = <"Endereço">
					description = <"Endereços vinculados a um único contato, ou seja, com o mesmo período de validade">
				>
				["at0040"] = <
					text = <"Grau de parentesco">
					description = <"Define o grau de parentesco entre as pessoas envolvidas">
				>
			>
		>
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Person">
					description = <"Personal demographic data">
				>
				["at0001"] = <
					text = <"Demographic details">
					description = <"A person's demographic details.">
				>
				["at0002"] = <
					text = <"Name">
					description = <"A person's name.">
				>
				["at0003"] = <
					text = <"Contacts">
					description = <"A person's contacts.">
				>
				["at0004"] = <
					text = <"Relationships">
					description = <"A person's relationships, especially family ties.">
				>
				["at0010"] = <
					text = <"Birth, death, documentation and other demographic data">
					description = <"Allows the collection of data about birth and death data, documentation and other person's demographic data.">
				>
				["at0030"] = <
					text = <"Addresses">
					description = <"Addresses linked to a single contact, i.e., with the same time validity.">
				>
				["at0040"] = <
					text = <"Relationship type">
					description = <"Defines the type of relationship between related persons.">
				>
			>
		>
	>
	constraint_definitions = <
		["pt-br"] = <
			items = <
				["ac0000"] = <
					text = <"Códigos para tipo de parentesco">
					description = <"códigos válidos para tipo de parentesco">
				>
			>
		>
		["en"] = <
			items = <
				["ac0000"] = <
					text = <"Codes for type of relationship">
					description = <"Valid codes for type of relationship.">
				>
			>
		>
	>
