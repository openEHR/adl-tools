archetype (adl_version=1.4)
	openEHR-EHR-EVALUATION.substance_use_summary.v1

concept
	[at0000]	-- Substance Use Summary
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"21/06/2009">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"This is a generic archetype used to record an ongoing and persistent summary of the use of any and all substances. ">
			use = <"This is a generic archetype used to record an ongoing and persistent summary of the use of any and all substances that have not had specific specialisation archetypes created. The typical use will be related to a summary of use or consumption of substances.  ">
			keywords = <"consumption", "substance", "use", "usage", "dependence">
			misuse = <"Do not use this archeytpe for recording actual details about the use of the substance and event-related data - this should be captured using the OBSERVATION.substance_use archetype.  Do not use this archetype for recording use or consumption histories for common substances such as tobacco smoking and alcohol.  As substances such as tobacco and alcohol are so commonly consumed, data should be recorded using detailed and specific specialisations of this archetype.">
			copyright = <"copyright (c) 2010 openEHR Foundation">
		>
	>
	lifecycle_state = <"0">
	other_contributors = <"Sam Heard, Ocean Informatics, Australia", ...>
	other_details = <
		["MD5-CAM-1.0.1"] = <"143877F2918D324E8871EEAED0166B11">
		["references"] = <"">
	>

definition
	EVALUATION[at0000] matches {	-- Substance Use Summary
		data matches {
			ITEM_TREE[at0001] matches {	-- Tree
				items cardinality matches {0..*; unordered} matches {
					ELEMENT[at0005] occurrences matches {0..1} matches {	-- Substance
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0002] occurrences matches {0..1} matches {	-- Usage Status
						value matches {
							DV_CODED_TEXT matches {
								defining_code matches {
									[local::
									at0003, 	-- Current User
									at0004, 	-- Former Regular User
									at0006, 	-- Former Occasional User
									at0016]	-- Never Used
								}
							}
						}
					}
					CLUSTER[at0007] occurrences matches {0..*} matches {	-- Consumption Summary
						items cardinality matches {0..*; unordered} matches {
							ELEMENT[at0008] occurrences matches {0..1} matches {	-- Form
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0015] occurrences matches {0..1} matches {	-- Method of use
								value matches {
									DV_TEXT matches {*}
								}
							}
							ELEMENT[at0009] occurrences matches {0..1} matches {	-- Date commenced
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0010] occurrences matches {0..1} matches {	-- Age commenced
								value matches {
									DV_DURATION matches {*}
								}
							}
							ELEMENT[at0011] occurrences matches {0..1} matches {	-- Date ceased
								value matches {
									DV_DATE_TIME matches {*}
								}
							}
							ELEMENT[at0012] occurrences matches {0..1} matches {	-- Age ceased
								value matches {
									DV_DURATION matches {*}
								}
							}
							ELEMENT[at0013] occurrences matches {0..1} matches {	-- Comment
								value matches {
									DV_TEXT matches {*}
								}
							}
						}
					}
					allow_archetype CLUSTER[at0014] occurrences matches {0..*} matches {	-- Cessation attempts
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.cessation_attempts\.v1/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Substance Use Summary">
					description = <"Generic archetype to record a persisting summary or overview of use or consumption of a defined substance over time. ">
				>
				["at0001"] = <
					text = <"Tree">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Usage Status">
					description = <"Overview of usage of the substance">
				>
				["at0003"] = <
					text = <"Current User">
					description = <"Person is a current user of the substance">
				>
				["at0004"] = <
					text = <"Former Regular User">
					description = <"Person a former or ex-user of the substance on a regular basis">
				>
				["at0005"] = <
					text = <"Substance">
					description = <"Identification of the substance ">
				>
				["at0006"] = <
					text = <"Former Occasional User">
					description = <"Person a former or ex-user of the substance on a regular basis">
				>
				["at0007"] = <
					text = <"Consumption Summary">
					description = <"Summary of the pattern of use for any and all forms of the substance, and for multiple episodes of use">
				>
				["at0008"] = <
					text = <"Form">
					description = <"Identify the form of the substance">
				>
				["at0009"] = <
					text = <"Date commenced">
					description = <"Date that use of the substance commenced (can be a partial date, such as a year)">
				>
				["at0010"] = <
					text = <"Age commenced">
					description = <"Age that use of the substance commenced">
				>
				["at0011"] = <
					text = <"Date ceased">
					description = <"Date that use of the substance ceased (can be a partial date, such as a year)">
				>
				["at0012"] = <
					text = <"Age ceased">
					description = <"Age that use of the substance ceased">
				>
				["at0013"] = <
					text = <"Comment">
					description = <"Comment on the usage of the substance">
				>
				["at0014"] = <
					text = <"Cessation attempts">
					description = <"Details about attempts to cease use of the substance">
				>
				["at0015"] = <
					text = <"Method of use">
					description = <"Method of use or consumption of the substance">
				>
				["at0016"] = <
					text = <"Never Used">
					description = <"Person has never used the substance">
				>
			>
		>
	>
