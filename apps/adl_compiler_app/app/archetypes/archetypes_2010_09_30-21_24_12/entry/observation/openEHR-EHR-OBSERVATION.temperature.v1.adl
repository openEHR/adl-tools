archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.temperature.v1

concept
	[at0000]	-- Temperature
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
		["date"] = <"18/09/2008">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the measured temperature of an identified object.">
			use = <"Used for recording the specific temperature of an identified object.">
			keywords = <"temperature", ...>
			misuse = <"This temperature is not to be used to record the body temperature, which is a surrogate for the whole body temperature of the subject - use OBSERVATION.body_temperature instead in this situation.">
			copyright = <"copyright (c) 2010 openEHR Foundation">
		>
	>
	lifecycle_state = <"AuthorDraft">
	other_contributors = <>
	other_details = <
		["references"] = <"">
	>

definition
	OBSERVATION[at0000] matches {	-- Temperature
		data matches {
			HISTORY[at0001] matches {	-- Event Series
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0002] occurrences matches {0..1} matches {	-- Any event
						data matches {
							ITEM_LIST[at0003] matches {	-- Single
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0006] occurrences matches {0..1} matches {	-- Object
										value matches {
											DV_TEXT matches {*}
										}
									}
									ELEMENT[at0004] matches {	-- Temperature
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::127]>
												list = <
													["1"] = <
														units = <"°C">
													>
													["2"] = <
														units = <"°F">
														precision = <|1|>
													>
												>
											>
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0007] matches {	-- List
				items cardinality matches {0..*; unordered} matches {
					allow_archetype CLUSTER[at0009] occurrences matches {0..*} matches {	-- Device
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Temperature">
					description = <"A measurement of temperature of a specified object">
				>
				["at0001"] = <
					text = <"Event Series">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"Any event">
					description = <"*">
				>
				["at0003"] = <
					text = <"Single">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Temperature">
					description = <"The measured temperature of the object">
				>
				["at0006"] = <
					text = <"Object">
					description = <"Identify the object for which the temperature is being measured.">
				>
				["at0007"] = <
					text = <"List">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Device">
					description = <"Details of the device used to measure the temperature">
				>
			>
		>
	>
