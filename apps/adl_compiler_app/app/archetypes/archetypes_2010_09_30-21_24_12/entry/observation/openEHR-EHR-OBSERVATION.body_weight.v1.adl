archetype (adl_version=1.4)
	openEHR-EHR-OBSERVATION.body_weight.v1

concept
	[at0000]	-- Body weight
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["de"] = <
			language = <[ISO_639-1::de]>
			author = <
				["name"] = <"Sebastian Garde, Jasmin Buck">
				["organisation"] = <"Ocean Informatics, University of Heidelberg">
			>
		>
		["nl"] = <
			language = <[ISO_639-1::nl]>
			author = <
				["name"] = <"Marja Buur">
				["organisation"] = <"Medisch Centrum Alkmaar, Nederland">
				["email"] = <"m.buur-krom@mca.nl">
			>
			accreditation = <"Nurse informatics">
		>
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			author = <
				["name"] = <"Marco Borges">
				["organisation"] = <"P2D">
				["email"] = <"marco.borges@p2d.com.br">
			>
			accreditation = <"P2D Health Advisor Council">
		>
		["ru"] = <
			language = <[ISO_639-1::ru]>
			author = <
				["name"] = <"Igor Lizunov">
				["email"] = <"i.lizunov@infinnity.ru">
			>
		>
	>
description
	original_author = <
		["name"] = <"Sam Heard">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"sam.heard@oceaninformatics.com">
		["date"] = <"9/03/2006">
	>
	details = <
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Para registrar o peso corporal de um indivíduo - tanto real como aproximado.">
			use = <"Usado para gravar a medição real de peso corporal, inclusive quando o indivíduo tem faltando uma parte do corpo devido a uma causa congênita ou após a remoção cirúrgica. A indicação da imperfeição física do corpo pode ser registrada no elemento 'fatores de erro', se necessário.
Este é o arquétipo para ser utilizado para uma medição típica de peso, por exemplo, auto-medido pelo indivíduo em casa, uma medida médico em uma clínica / hospital, ou um instrutor de fitness em um ginásio. 

Também pode ser usado para a gravar uma aproximação da medição do peso corporal em um cenário clínico no qual não é possível medir com precisão o peso do corpo - por exemplo, pesar uma criança inquieta, ou estimar o peso de um feto (quando 'sujeito' é um feto e a gravação ocorre no registro da saúde da mãe). Isso não é modelado explicitamente no arquétipo como o modelo de referência da openEHR permite que o atributo de aproximação para qualquer tipo de dados quantitativos. Na execução, por exemplo, uma interface de usuário do aplicativo pode permitir que os clínicos para selecionar uma caixa de seleção devidamente setados junto ao campo de dados de peso, indicando que o peso verificado é uma aproximação, ao invés de reais. 

Usada para gravar a mudança de peso, ou seja, qualquer perda ou ganho de peso. Pode ser modelado por restringir a 'qualquer evento' a um intervalo associado com funções matemáticas de aumentar ou diminuir, conforme o caso.">
			keywords = <"*weight(en)", "*gain(en)", "*loss(en)", "*increase(en)", "*decrease(en)", "*mass(en)", "*estimate(en)", "*actual(en)">
			misuse = <"Não deve ser utilizado para gravar o primeiro peso de um bebê logo após o nascimento, que é designado como o seu 'peso' - use a especialização de seu nascimento arquétipo OBSERVATION.body_weight-birth.
Não deve ser usado para registrar o peso do corpo ajustado por exemplo, um cálculo do peso de corpo inteiro de uma pessoa com amputação de membros, com base em medições de outro corpo e um algoritmo -OBSERVATION.body_weight-adjusted.
Não deve ser usado para registrar o peso de um objeto ou parte do corpo.">
			copyright = <"copyright (c) 2010 openEHR Foundation">
		>
		["de"] = <
			language = <[ISO_639-1::de]>
			purpose = <"Zur Dokumentation des Gewichtes eines Individuums, sowohl exakt als auch ungefähr.">
			use = <"Zur Dokumentation des tatsächlichen Körpergewichts, auch wenn das Individuum einen Körperteil (angeboren oder später amputiert) vermisst. Sofern benötigt, kann dies im Datenelement 'Störfaktoren' dokumentiert werden. Dies ist der Archetyp, der gewöhnlicherweise für eine typische Gewichtsmessung benutzt werden sollte, z.B. bei Selbstmessung durch das Individuum zu Hause, durch einen Kliniker im Krankenhaus, oder einen Fitness-Trainer in einem Fitness-Center.

Der Archetyp kann auch benutzt werden, um eine Schätzung des Körpergewichts zu dokumentieren, wenn es nicht möglich ist, das Gewicht genau zu bestimmen - z.B. bei der Messung des Gewichts eines nicht kooperativen Kindes, oder bei einem ungeborenen Fötus (hier ist das 'Subjekt der Daten' der Fötus und die Dokumentation erfolgt in der Akte der Mutter). Dass es sich um eine Schätzung handelt wird in diesem Archetyp nicht explizit modelliert, da das openEHR Referenzmodell dies direkt für 'Quantity'-Datentypen unterstützt. In einer konkreten klinischen Anwendung könnte die Benutzerschnittstelle es dem Kliniker z.B. über eine Checkbox ermöglichen, zu dokumentieren, dass es sich um eine Schätzung handelt.">
			keywords = <"Gewicht", "Zunahme", "Verlust", "Masse", "Schätzung">
			misuse = <"Nicht zur Dokumentation des ersten Gewichts eines Neugeboren (Geburtsgewicht) - benutzen Sie hierzu den spezialisierten Archetyp OBSERVATION.body_weight-birth.
Nicht zur Dokumentation des angepassten Körpergewichts, z.B. eine Berechnung des vollständigen Körpergewichts bei einer Person mit amputierter Extremität auf Basis der anderen Körperteile und eines Algorithmus - benutzen Sie OBSERVATION.body_weight-adjusted.
Nicht zur Dokumentation eines Objekts oder eines Teils des Körpers.">
			copyright = <"copyright (c) 2010 openEHR Foundation">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the body weight of an individual - both actual and approximate.">
			use = <"To be used for recording the actual measurement of body weight, including when the individual is missing a body part due to a congenital cause or after surgical removal.   A statement identifying the physical incompleteness of the body can be recorded in the 'Confounding factors' data element, if required. This is the usual archetype to be used for a typical measurement of weight, for example self-measured by the individual at home, a clinician measurement in a clinic/hospital, or a fitness instructor in a gymnasium.  

Can also be used for recording an approximation of body weight measurement in a clinical scenario where it is not possible to measure accurately body weight - for example, weighing an uncooperative child, or estimating the weight of an unborn fetus (where the 'subject of data' is the Fetus and recording occurs within the mother's health record).  This is not modelled explicitly in the archetype as the openEHR Reference model allows the attribute of Approximation for any Quantity data type.  At implementation, for example, an application user interface could allow clinicians to select an appropriately labelled check box adjacent to the Weight data field to indicate that the recorded weight is an approximation, rather than actual.

To be used for recording weight change, that is, either weight loss or weight gain.  This can currently be modelled by constraining the 'any event' to an interval with associated mathematical function of increase or decrease, as appropriate.">
			keywords = <"weight", "gain", "loss", "increase", "decrease", "mass", "estimate", "actual">
			misuse = <"Not to be used to record the first weight of an infant soon after birth which is designated as their 'birth weight' - use the specialisation of this archetype OBSERVATION.body_weight-birth.
Not to be used to record the adjusted body weight eg a calculation of the full body weight of a person with limb amputation, based on other body part measurements and an algorithm - use OBSERVATION.body_weight-adjusted.
Not to be used to record the weight of an object or body part.">
			copyright = <"copyright (c) 2010 openEHR Foundation">
		>
		["ru"] = <
			language = <[ISO_639-1::ru]>
			purpose = <"Для записи массы тела человека: фактической или приблизительной.">
			use = <"Использовать для записи фактического измерения массы тела, включая случаи отсутсвия части(-ей) тела, врожденное или после хирургического удаления. Отметка о физической неполноте тела может быть зарегистрирована в элементе данных \"стохастическая погрешность\", если требуется. Это - обычный архетип, используемый для типичного измерения веса, например самоизмеренного человеком дома, измерение клинициста в клинике/больнице, или фитнес-инструктором в гимнастическом зале. 

Может также использоваться для записи примерного измерения массы тела в клиническом сценарии, где не возможно взвешивание - например, сопротивляющийся ребёнок, или для оценики веса внутриутробного плода (где 'предметом данных' является плод, и регистрация происходит в пределах записи о состоянии здоровья матери). Это не оформлено явно в архетипе, поскольку модель openEHR позволяет атрибут «приблизительно» для любого типа данных «количество». При работе, например, прикладной пользовательский интерфейс позволяет клиницистам выбирать соответствующую отметку, смежную с полем данных «вес», чтобы указать, что зарегистрированный вес - приблизительный, а не фактический. 

Использовать для записи изменения веса, то есть, потери веса или увеличения веса.
Это может в настоящее время моделироваться, привязывая 'каждый случай' к интервалу со связанной математической функцией увеличения или уменьшения, соответственно.">
			keywords = <"вес", "масса тела", "прибавка", "потеря", "увеличение", "уменьшение", "оценка", "актуально">
			misuse = <"Не использовать для записи первого веса младенца после рождение, которое обозначено как 'вес при рождении' - использовать специализацию этого архетипа OBSERVATION.body_weight-birth. 
Не использовать для записи массы тела человека с протезами / приспособлениями для вычисление полной массы тела человека с ампутацией, основанной на других измерениях и алгоритме  - использовать архетип OBSERVATION.body_weight-adjusted.
Не использовать, чтобы сделать запись веса части тела или объекта.">
			copyright = <"copyright (c) 2010 openEHR Foundation">
		>
		["nl"] = <
			language = <[ISO_639-1::nl]>
			purpose = <"Om het lichaamsgewicht van een persoon te registreren - zowel exact als geschat gewicht.">
			use = <"Registreren van een actuele meting van het lichaamsgewicht, ook als deze persoon een lichaamsdeel mist, door een geboorteafwijking of na een chirurgische ingreep. Een verklaring over de fysieke inclompleetheid van het lichaam, kan zo nodig opgeslagen worden in het data element 'beïnvloedende factoren'. Dit is het gebruikelijke archetype voor gewichtmetingen, bij voorbeeld thuis gemeten door de persoon zelf, een klinische meting in een kliniek/ziekenhuis, of door een fitness instructeur in een sportschool.

Kan ook gebruikt worden om een geschat lichaamsgewicht te registreren in een klinische setting als het niet mogelijk is om het exacte lichaamsgewicht te meten - bijvoorbeeld, het wegen van een tegenwerkend kind, of een schatting van het gewicht van een ongeboren kind (waar het onderwerp van de gegevens de foetus is en de opslag in het patiënten dossier van de moeder plaatsvindt). Dit is niet expliciet gemodelleerd in het archetype, omdat het openEHR Referentie model een schatting in ieder kwantitatief data type toestaat. Bij de uitvoering, bijvoorbeeld, zou een applicatie gebruikersinterface, clinici een adequaat geëtiketteerd selectievakje kunnen aanbieden, naast de gegevens over het gewicht, waarin door selecteren aangegeven kan worden dat het opgenomen gewicht een schatting is, in plaats van het werkelijke gewicht.

Dient te worden gebruikt om gewichtsverandering op te slaan, zowel gewichtsverlies als gewichtstoename.  Dit kan gemodelleerd worden door 'any event' - elke gebeurtenis - in voorkomende gevallen, te beperken tot een interval met met bijbehorende rekenkundige functie van stijging of daling.">
			keywords = <"gewicht", "stijging", "verlies", "toename", "daling", "massa", "schatting", "werkelijk", "afname">
			misuse = <"Dient niet te worden gebruikt voor het registreren van het eerste gewicht van een kind, na geboorte, welke wordt aangewezen als geboortegewicht. Gebruik hiervoor de specialisatie van dit archetype, OBSERVATION.body_weight-birth (OBSERVATION.lichaamsgewicht-geboorte.
Dient niet te worden gebruikt voor het registreren van het aangepaste lichaamsgewicht, bijvoorbeeld een berekening van het volledige lichaamsgewicht van een persoon met een amputatie van ledematen, gebaseerd op metingen van lichaamsdelen en een algoritme - gebruik hiervoor OBSERVATION.body_weight-adjusted. (OBSERVATION.lichaamsgewicht-aangepast).
Dient niet te worden gebruikt voor het vastleggen van het gewicht van een object of lichaamsdeel.">
			copyright = <"copyright (c) 2010 openEHR Foundation">
		>
	>
	lifecycle_state = <"Published">
	other_contributors = <"Jeroen Meintjens, Medisch Centrum Alkmaar, Netherlands", "Marja Buur-Krom, Medisch Centrum Alkmaar, Netherlands", "Sebastian Garde, Ocean Informatics, Germany", "Ian McNicoll, Ocean Informatics, Scotland", "Heather Leslie, Ocean Informatics, Australia", "Paul Donaldson, Queensland Health, Australia", "Heather Grain, Llewellyn Informatics, Australia", "Anne Harbison, Australia", "Andrew James, University of Toronto, Canada", "Rikard Lovstrom, Swedish Medical Association, Sweden", "Hans Demski, Helmholtz Zentrum München, Germany", "Soon Ghee Yap, Singapore Health Services Pte Ltd, Singapore">
	other_details = <
		["MD5-CAM-1.0.1"] = <"79B6211F7B1898E8A8567B4B418492F9">
	>

definition
	OBSERVATION[at0000] matches {	-- Peso corporal
		data matches {
			HISTORY[at0002] matches {	-- history
				events cardinality matches {1..*; unordered} matches {
					EVENT[at0003] occurrences matches {0..*} matches {	-- Qualquer evento
						data matches {
							ITEM_TREE[at0001] matches {	-- Simple
								items cardinality matches {1; unordered} matches {
									ELEMENT[at0004] matches {	-- Peso
										value matches {
											C_DV_QUANTITY <
												property = <[openehr::124]>
												list = <
													["1"] = <
														units = <"kg">
														magnitude = <|0.0..1000.0|>
													>
													["2"] = <
														units = <"lb">
														magnitude = <|0.0..2000.0|>
													>
												>
											>
										}
									}
									ELEMENT[at0024] occurrences matches {0..1} matches {	-- Comentário
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
						state matches {
							ITEM_TREE[at0008] matches {	-- state structure
								items cardinality matches {0..*; ordered} matches {
									ELEMENT[at0009] occurrences matches {0..1} matches {	-- Vestuário
										value matches {
											DV_CODED_TEXT matches {
												defining_code matches {
													[local::
													at0011, 	-- Levemente vestido / roupa íntimas
													at0013, 	-- Despido
													at0010, 	-- Totalmente vestida, incluindo sapatos
													at0017; 	-- Fralda
													at0011]	-- assumed value
												}
											}
										}
									}
									ELEMENT[at0025] occurrences matches {0..1} matches {	-- Fatores de erro
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0015] matches {	-- protocol structure
				items cardinality matches {0..*; ordered} matches {
					allow_archetype CLUSTER[at0020] occurrences matches {0..1} matches {	-- Dispositivo
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.device(-[a-zA-Z0-9_]+)*\.v1/}
					}
				}
			}
		}
	}


ontology
	term_definitions = <
		["nl"] = <
			items = <
				["at0000"] = <
					text = <"Lichaamsgewicht">
					description = <"Meting van het lichaamsgewicht van een individu.">
				>
				["at0001"] = <
					text = <"*Simple(en)">
					description = <"*@ internal @(en)">
				>
				["at0002"] = <
					text = <"*history(en)">
					description = <"*@ internal @(en)">
				>
				["at0003"] = <
					text = <"Elke gebeurtenis">
					description = <"Elke gebeurtenis.">
				>
				["at0004"] = <
					text = <"Gewicht">
					description = <"Het gewicht van het individu.">
				>
				["at0008"] = <
					text = <"*state structure(en)">
					description = <"*@ internal @(en)">
				>
				["at0009"] = <
					text = <"Hoeveelheid kleding">
					description = <"Beschrijving van de hoeveelheid kleding van de persoon, op het moment van wegen.">
				>
				["at0010"] = <
					text = <"Volledig gekleed, inclusief schoenen">
					description = <"Kleren die een significante bijdrage hebben aan het gewicht, inclusief schoenen.">
				>
				["at0011"] = <
					text = <"Lichte kleding/ondergoed">
					description = <"Kleding die niet significant het gewicht beÏnvloedt.">
				>
				["at0013"] = <
					text = <"Naakt">
					description = <"Zonder kleding.">
				>
				["at0015"] = <
					text = <"*protocol structure(en)">
					description = <"*@ internal @(en)">
				>
				["at0017"] = <
					text = <"Luier">
					description = <"Individu draagt alleen een luier - zou significant aan het gewicht kunnen bijdragen.">
				>
				["at0020"] = <
					text = <"Apparaat">
					description = <"Details over het weeginstrument.">
				>
				["at0024"] = <
					text = <"Opmerking">
					description = <"Opmerking over de gewichts meting.">
				>
				["at0025"] = <
					text = <"BeÏnvloedende factoren">
					description = <"Registreer eventuele problemen of factoren die van invloed kunnen zijn op de meting van het lichaamsgewicht bijvoorbeeld timing in de menstruele cyclus, timing van de recente stoelgang of het noteren van een amputatie.">
				>
			>
		>
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Body weight">
					description = <"Measurement of the body weight of an individual.">
				>
				["at0001"] = <
					text = <"Simple">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"history">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Any event">
					description = <"Any event.">
				>
				["at0004"] = <
					text = <"Weight">
					description = <"The weight of the individual.">
				>
				["at0008"] = <
					text = <"state structure">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"State of Dress">
					description = <"Description of the state of dress of the person at the time of weighing.">
				>
				["at0010"] = <
					text = <"Fully clothed, including shoes">
					description = <"Clothing which may add significantly to weight, including shoes.">
				>
				["at0011"] = <
					text = <"Lightly clothed/underwear">
					description = <"Clothing which will not add to weight significantly.">
				>
				["at0013"] = <
					text = <"Naked">
					description = <"Without any clothes.">
				>
				["at0015"] = <
					text = <"protocol structure">
					description = <"@ internal @">
				>
				["at0017"] = <
					text = <"Nappy/diaper">
					description = <"Wearing only a nappy - can add significant weight.">
				>
				["at0020"] = <
					text = <"Device">
					description = <"Details about the weighing device.">
				>
				["at0024"] = <
					text = <"Comment">
					description = <"Comment about the measurement of weight.">
				>
				["at0025"] = <
					text = <"Confounding Factors">
					description = <"Record any issues or factors that may impact on the measurement of body weight eg timing in menstrual cycle, timing of recent bowel motion or noting of amputation.">
				>
			>
		>
		["de"] = <
			items = <
				["at0000"] = <
					text = <"Körpergewicht">
					description = <"Messung des Körpergewichts eines Individuums.">
				>
				["at0001"] = <
					text = <"Simple">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"History">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"*Any event(en)">
					description = <"*Any event(en)">
				>
				["at0004"] = <
					text = <"Gewicht">
					description = <"Das Gewicht eines Individuums.">
				>
				["at0008"] = <
					text = <"State structure">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Bekleidung">
					description = <"Beschreibung der Bekleidung zum Zeitpunkt der Messung.">
				>
				["at0010"] = <
					text = <"Voll bekleidet, mit Schuhen">
					description = <"Bekleidung, die signifikant zum Gewicht beiträgt, mit Schuhen.">
				>
				["at0011"] = <
					text = <"Leicht bekleidet / Unterwäsche">
					description = <"Bekleidung, die nicht signifikant zum Gewicht beiträgt.">
				>
				["at0013"] = <
					text = <"Unbekleidet">
					description = <"Ohne Kleidung.">
				>
				["at0015"] = <
					text = <"Protocol structure">
					description = <"@ internal @">
				>
				["at0017"] = <
					text = <"Windel">
					description = <"Trägt Windel; kann signifikant zum Gewicht beitragen.">
				>
				["at0020"] = <
					text = <"Gerät">
					description = <"Details über die benutzte Waage.">
				>
				["at0024"] = <
					text = <"Kommentar">
					description = <"Kommentar über die Messung des Gewichts.">
				>
				["at0025"] = <
					text = <"Störfaktoren">
					description = <"Zur Dokumentation von Faktoren, die einen Einfluss auf die Messung des Körpergewichts haben können, z.B. Zeitpunkt in der Menstruationsperiode, Zeitpunkt des letzten Stuhlgangs, durchgeführte Amputationen.">
				>
			>
		>
		["pt-br"] = <
			items = <
				["at0000"] = <
					text = <"Peso corporal">
					description = <"A medição do peso corporal de um indivíduo.">
				>
				["at0001"] = <
					text = <"Simple">
					description = <"@ internal @">
				>
				["at0002"] = <
					text = <"history">
					description = <"@ internal @">
				>
				["at0003"] = <
					text = <"Qualquer evento">
					description = <"Qualquer evento.">
				>
				["at0004"] = <
					text = <"Peso">
					description = <"O peso do indivíduo.">
				>
				["at0008"] = <
					text = <"state structure">
					description = <"@ internal @">
				>
				["at0009"] = <
					text = <"Vestuário">
					description = <"Descrição do vestuário da pessoa na hora da pesagem.">
				>
				["at0010"] = <
					text = <"Totalmente vestida, incluindo sapatos">
					description = <"Roupas que podem aumentar significativamente o peso, incluindo sapatos.">
				>
				["at0011"] = <
					text = <"Levemente vestido / roupa íntimas">
					description = <"Roupas que não irão acrescentar ao peso de forma significativa.">
				>
				["at0013"] = <
					text = <"Despido">
					description = <"Sem nenhuma roupa.">
				>
				["at0015"] = <
					text = <"protocol structure">
					description = <"@ internal @">
				>
				["at0017"] = <
					text = <"Fralda">
					description = <"Vestindo apenas uma fralda - pode adicionar peso significativo.">
				>
				["at0020"] = <
					text = <"Dispositivo">
					description = <"Detalhes sobre o dispositivo de pesagem.">
				>
				["at0024"] = <
					text = <"Comentário">
					description = <"Comentário sobre a medição do peso.">
				>
				["at0025"] = <
					text = <"Fatores de erro">
					description = <"Registrar quaisquer problemas ou fatores que possam ter impacto sobre a medição de peso corporal, por exemplo, no momento do ciclo menstrual, o ciclo intestinal ou anotando amputação.">
				>
			>
		>
		["ru"] = <
			items = <
				["at0000"] = <
					text = <"Масса тела">
					description = <"Взвешивание пациента.">
				>
				["at0001"] = <
					text = <"*Simple(en)">
					description = <"*@ internal @(en)">
				>
				["at0002"] = <
					text = <"*history(en)">
					description = <"*@ internal @(en)">
				>
				["at0003"] = <
					text = <"Любое событие">
					description = <"Любое событие.">
				>
				["at0004"] = <
					text = <"Вес">
					description = <"Вес пациента.">
				>
				["at0008"] = <
					text = <"*state structure(en)">
					description = <"*@ internal @(en)">
				>
				["at0009"] = <
					text = <"Наличие и характер одежды">
					description = <"Описание наличия и характера одежды пациента во время взвешивания.">
				>
				["at0010"] = <
					text = <"В одежде без обуви">
					description = <"Одежда может добавить значительный вес.">
				>
				["at0011"] = <
					text = <"В лёгкой одежде или раздевшись до белья">
					description = <"Одежда, не добавляющая значительный вес.">
				>
				["at0013"] = <
					text = <"Обнажен">
					description = <"Без какой-либо одежды и белья.">
				>
				["at0015"] = <
					text = <"*protocol structure(en)">
					description = <"*@ internal @(en)">
				>
				["at0017"] = <
					text = <"В памперсе">
					description = <"Одет(а) только в памперс - может добавить значительный вес.">
				>
				["at0020"] = <
					text = <"Устройство">
					description = <"Весы (устройство, на котором производилось взвешивание): информация.">
				>
				["at0024"] = <
					text = <"Комментарии">
					description = <"Комментарии к взвешиванию.">
				>
				["at0025"] = <
					text = <"Стохастическая погрешность">
					description = <"Фиксация любых фактов, могущих повлиять на результат взвешивания: фаза менструального цикла, давность опорожнения кишечника, сведения об ампутации и др.">
				>
			>
		>
	>
